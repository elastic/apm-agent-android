plugins {
    id 'java-gradle-plugin'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

targetCompatibility = jvmCompatibility
sourceCompatibility = jvmCompatibility

sourceSets.create('functionalTest')

configurations {
    testPluginDependency {
        canBeResolved(false)
        canBeConsumed(false)
    }
    testPluginClasspath {
        canBeConsumed(false)
        canBeResolved(true)
        extendsFrom(testPluginDependency)
    }
}

dependencies {
    implementation "net.bytebuddy:byte-buddy-gradle-plugin:$bytebuddy_version"
    implementation "net.bytebuddy:byte-buddy:$bytebuddy_version"
    implementation project(':android-common')
    compileOnly "com.android.tools.build:gradle:7.2.0"
    functionalTestImplementation "junit:junit:$junit_version"
    functionalTestImplementation "com.github.stefanbirkner:system-rules:1.19.0"
    functionalTestImplementation project(':android-common')
    testPluginDependency "com.android.tools.build:gradle:7.2.0"
}

tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.testPluginClasspath)
}

buildConfig {
    packageName("${group}.generated")
    buildConfigField("String", "SDK_DEPENDENCY_URI", "\"$group:android-sdk:$version\"")
    buildConfigField("String", "SDK_KTX_DEPENDENCY_URI", "\"$group:android-sdk-ktx:$version\"")
    buildConfigField("String", "INSTRUMENTATION_DEPENDENCY_URI", "\"$group:android-instrumentation:$version\"")
}

tasks.register('functionalTest', Test) {
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

licensesConfig {
    manualMappingFile = rootProject.file("manual_licenses_map.txt")
}

gradlePlugin {
    plugins {
        apmPlugin {
            id = 'co.elastic.apm.android'
            implementationClass = 'co.elastic.apm.android.plugin.ApmAndroidAgentPlugin'
            displayName = "Elastic APM Android Agent"
            description = project.description
        }
    }
}