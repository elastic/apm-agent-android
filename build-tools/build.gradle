plugins {
    id "java-gradle-plugin"
}

Properties properties = new Properties()
File propertiesFile = new File(rootDir, '../gradle.properties')
propertiesFile.withInputStream {
    properties.load(it)
}

dependencies {
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'commons-io:commons-io:2.13.0'
    implementation "com.android.tools.build:gradle:${properties["androidGradlePlugin_version"]}"

    // This is needed to enforce a higher version than the transitive one (3.0.0) requested by: "com.diffplug.spotless:spotless-plugin-gradle:6.21.0"
    implementation "com.squareup.okio:okio:3.5.0"
    implementation "com.diffplug.spotless:spotless-plugin-gradle:6.22.0"
    implementation "org.jetbrains.dokka:dokka-gradle-plugin:1.9.0"
    implementation "io.github.gradle-nexus:publish-plugin:1.3.0"
    implementation 'com.gradle.publish:plugin-publish-plugin:1.2.1'
    testImplementation "junit:junit:4.13.2"
}

gradlePlugin {
    plugins {
        publishingPlugin {
            id = 'co.elastic.apm.publishing'
            implementationClass = "co.elastic.apm.compile.tools.publishing.ApmPublisherRootPlugin"
        }
        dependencyEmbedderPlugin {
            id = 'co.elastic.apm.dependency.embedder'
            implementationClass = "co.elastic.apm.compile.tools.embedding.EmbeddingDependenciesPlugin"
        }
    }
}