plugins {
    id 'java-gradle-plugin'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

targetCompatibility = jvmCompatibility
sourceCompatibility = jvmCompatibility

sourceSets.create('functionalTest')

dependencies {
    implementation "net.bytebuddy:byte-buddy-gradle-plugin:$bytebuddy_version"
    implementation "net.bytebuddy:byte-buddy:$bytebuddy_version"
    implementation project(':android-common')
    implementation project(':agp-compatibility:agp-compatibility-api')
    implementation project(':agp-compatibility:agp-compatibility-7-2')
    implementation project(':agp-compatibility:agp-compatibility-7-3')
    compileOnly "com.android.tools.build:gradle:$androidGradlePlugin_version"
    functionalTestImplementation "junit:junit:$junit_version"
    functionalTestImplementation "com.github.stefanbirkner:system-rules:1.19.0"
    functionalTestImplementation project(':android-common')
}

buildConfig {
    packageName("${group}.generated")
    buildConfigField("String", "SDK_DEPENDENCY_URI", "\"$group:android-sdk:$version\"")
    buildConfigField("String", "SDK_KTX_DEPENDENCY_URI", "\"$group:android-sdk-ktx:$version\"")
    buildConfigField("String", "INSTRUMENTATION_DEPENDENCY_URI", "\"$group:android-instrumentation:$version\"")
}

tasks.register('functionalTest', Test) {
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

licensesConfig {
    manualMappingFile = rootProject.file("manual_licenses_map.txt")
}

gradlePlugin {
    plugins {
        apmPlugin {
            id = 'co.elastic.apm.android'
            implementationClass = 'co.elastic.apm.android.plugin.ApmAndroidAgentPlugin'
            displayName = "Elastic APM Android Agent"
            description = project.description
        }
    }
}