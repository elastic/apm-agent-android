plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'co.elastic.apm.dependency.embedder'
}

android {
    namespace = "co.elastic.apm.android.sdk"
    compileSdk androidCompileSdk
    buildFeatures.buildConfig true

    defaultConfig {
        minSdk androidMinSdk
        targetSdk androidCompileSdk

        buildConfigField("String", "APM_AGENT_VERSION", "\"${project.version}\"")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "../shared-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility jvmCompatibility
        targetCompatibility jvmCompatibility
        coreLibraryDesugaringEnabled = true
    }
    kotlinOptions {
        jvmTarget = jvmCompatibility.toString()
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

licensesConfig {
    manualMappingFile = rootProject.file("manual_licenses_map.txt")
}

shadowJar {
    relocate 'com.instacart.library.truetime', 'co.elastic.apm.android.truetime'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperty("agent_version", project.version)
}

dependencies {
    api libs.opentelemetry.sdk
    api libs.opentelemetry.android
    api libs.opentelemetry.api.incubator
    api libs.stagemonitor.configuration
    api libs.okhttp
    implementation project(':android-common')
    implementation libs.opentelemetry.android.instrumentation.api
    implementation libs.opentelemetry.android.instrumentation.lifecycle
    implementation libs.androidx.lifecycle
    implementation libs.weaklockfree
    implementation libs.opentelemetry.exporter.otlp
    implementation libs.opentelemetry.semconv
    implementation libs.opentelemetry.diskBuffering
    implementation libs.androidx.annotations
    implementation libs.dsl.json
    embedded libs.truetime
    annotationProcessor 'co.elastic.apm.compile:processor'
    compileOnly 'co.elastic.apm.compile:processor'
    testImplementation libs.bundles.mocking
    testImplementation libs.bundles.junit
    testImplementation libs.mockwebserver
    testImplementation libs.opentelemetry.testing
    testImplementation libs.robolectric
    testImplementation libs.assertj
    testRuntimeOnly libs.junit5.vintage
    coreLibraryDesugaring(libs.coreLib)
}